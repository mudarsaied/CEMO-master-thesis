TITLE:
    M2 Full model


DATA:
    file = "C:\Users\Mudar Saied\implist.dat";

    type = imputation;


VARIABLE:
   names =
        CNTRYID CNTSCHID CNTSTUID W_STU READ
        ! Demographic control var
        FEMALE IMMI1GEN IMMI2GEN ESCS
        ! ICT Use at Home
        ICTLEIS ICTSCHWK USESCH
        ! Reading Mediation
        READJOY READCOMP  READEASE 
        ! School Weight
        W_SCH
        ! School ICT Policy
        Q05HA
        ;
    
    usevar =
        ! Outcome
        READ
        ! Student-level
        FEMALE IMMI1GEN IMMI2GEN ESCS   ! Demographic controls
            ! ICT-related var
        ICTLEIS ICTSCHWK USESCH         ! ICT use at home/at school
            ! Mediators
        READJOY READCOMP READEASE       ! Reading affect
        ! School-level
        Q05HA                           ! School ICT policy
    ;
        
    within =
        FEMALE IMMI1GEN IMMI2GEN        ! Demographic controls
        READJOY READCOMP READEASE       ! Reading affect
    ;

    between =
        Q05HA                           ! School ICT policy
    ;

    cluster = CNTSCHID;                 ! Cluster by school ID

    missing = all (-99);

    weight = W_STU;                     ! Student weight
    wtscale = cluster;                  ! Scale L1 weight to cluster
    bweight = W_SCH;                    ! School weight
    bwtscale = sample;                  ! Scale L2 weight to sample


ANALYSIS:
    processors = 8;

    type = twolevel;


MODEL:

%Within%
! SES onto X vars
    ICTLEIS on ESCS FEMALE;
    ICTSCHWK on ESCS FEMALE;
    USESCH on ESCS FEMALE;
    
    ! Mediators
        ! Enjoyment of reading
    READJOY on ICTLEIS ICTSCHWK USESCH
               FEMALE ESCS IMMI1GEN IMMI2GEN;
               

        ! Perception of reading competence
         READCOMP on ICTLEIS ICTSCHWK USESCH
                     FEMALE ESCS IMMI1GEN IMMI2GEN;

        ! Perception of reading difficulty
    READEASE on ICTLEIS ICTSCHWK USESCH
                FEMALE ESCS IMMI1GEN IMMI2GEN;

    ! Total effect
    READ on                             ! Y
        ICTLEIS ICTSCHWK USESCH         ! X
        READJOY READCOMP READEASE       ! M
        FEMALE ESCS                     ! controls
    ;

    ! Correlations

    ! Correlation amongst X
    ICTLEIS with ICTSCHWK;
    ICTSCHWK with USESCH;
    ICTLEIS with USESCH;

    ! Correlation amongst M
    READJOY with READCOMP;
    READCOMP with READEASE;
    READEASE with READJOY;

    ! Correlation M with Y
    READJOY with READ;

%Between%

! Measurement part

    ! Lat var: School ICT policy
    !SCHPOL by SC156Q01 SC156Q02 SC156Q05 SC156Q06;

! Structural part

    ! Total effect
    READ on
        Q05HA                           ! School ICT policy
        ICTLEIS ICTSCHWK  USESCH        ! From L1 <- contextual effect
        ESCS                            ! School average SES
    ;


MODEL INDIRECT:

    READ ind ICTLEIS;
    READ ind ICTSCHWK;
    REad ind USESCH;


OUTPUT:
    
    mod (all);
