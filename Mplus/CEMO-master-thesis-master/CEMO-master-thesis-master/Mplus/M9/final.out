Mplus VERSION 8.3
MUTHEN & MUTHEN
02/28/2021   2:21 PM

INPUT INSTRUCTIONS


  TITLE:
      Final: Two-level structured model for all countries

  DATA:
      file = "C:\Users\tctan\implist.dat";

      type = imputation;


  VARIABLE:
      names =
          FKI CNTRYID CNTSCHID W_STU
          MATH READ FLIT                      ! PISA achievement variables
          MALE IMMI1GEN IMMI2GEN ESCS         ! Demographic info
          FCFMLRTY FLCONFIN                   ! Affect
          FLSCHOOL                            !   Lat var "Academic"
          NOBULLY                             !   Lat var "Safety"
          FLFAMILY                            !   Lat var "Community"
          W_SCH STRATIO                       ! School character
          EDUSHORT                            !   Lat var "inst. env."
          ;

      usevar =
          FLIT                                ! PISA achievement variables
          FLSCHOOL                            !   Lat var "Academic"
          NOBULLY                             !   Lat var "Safety"
          FLFAMILY                            !   Lat var "Community"
          EDUSHORT STRATIO                    !   Lat var "Inst env"
          FCFMLRTY FLCONFIN                   ! Affect
          MALE IMMI1GEN IMMI2GEN ESCS         ! Demographic info
          ;

      ! Vars that ONLY appear in L1
      within =
          FCFMLRTY FLCONFIN                   ! Affective vars
          MALE IMMI1GEN IMMI2GEN ESCS         ! L1 control vars
          ;

      ! Vars that ONLY appear in L2
      between =
          EDUSHORT STRATIO                    ! L2: school
          ;

      weight = W_STU;                         ! Student weight
      wtscale = cluster;                      ! Scale L1 weight to cluster size
      bweight = W_SCH;                        ! School weight
      bwtscale = sample;                      ! Scale L2 weight to sample

      cluster = CNTSCHID;                     ! Cluster by school ID


  ANALYSIS:
      processors = 4;

      type = twolevel;


  MODEL:

  %Within%                                    ! === L1: Student-level ===

      ! Indirect pathways (1st half): school climate vars onto mediators
          ! Onto mediator FAMILIARITY
      FCFMLRTY on
          FLSCHOOL NOBULLY FLFAMILY
          MALE IMMI2GEN ESCS
          ;
          ! Onto mediator CONFIDENCE
      FLCONFIN on
          FLSCHOOL NOBULLY FLFAMILY
          MALE IMMI2GEN ESCS
          ;

      ! Total effect
      FLIT on
          ! Direct pathways: school climate vars onto financial literacy
          FLSCHOOL NOBULLY FLFAMILY
          ! Indirect pathways (2nd half): affective vars onto financial literacy
          FCFMLRTY FLCONFIN
          ! Control vars (demographic info)
          IMMI1GEN ESCS
          ;

      ! Covariances
          ! Between school climate vars
      FLSCHOOL with NOBULLY;
      FLSCHOOL with FLFAMILY;
      NOBULLY with FLFAMILY;
          ! Between mediators
      FCFMLRTY with FLCONFIN;

      ! Covariances suggested by modification indices
          ! ESCS with FLSCHOOL
      ESCS with FLSCHOOL;
          ! ESCS with FLFAMILY
      ESCS with FLFAMILY;
          ! ESCS with IMMI1GEN and IMMI2GEN
      ESCS with IMMI1GEN IMMI2GEN;

  %Between%                                   ! === L2: School-level ===

      FLIT on
          FLSCHOOL NOBULLY FLFAMILY EDUSHORT  ! School climate variables
          STRATIO                             ! Control: Student-teacher ratio
          ;

      FLSCHOOL with NOBULLY;
      NOBULLY with FLFAMILY;
      FLFAMILY with EDUSHORT;

      FLSCHOOL with FLFAMILY;
      NOBULLY with EDUSHORT;

      FLSCHOOL with EDUSHORT;


  MODEL INDIRECT:

      ! Indirect effects
      FLIT ind FLSCHOOL;
      FLIT ind NOBULLY;
      FLIT ind FLFAMILY;


  OUTPUT:
      stdyx                                   ! Fully standardised solution
      mod (all);

*** ERROR in DATA command
  The file specified for the FILE option cannot be found.  Check that this
  file exists:  C:\Users\tctan\implist.dat


MUTHEN & MUTHEN
3463 Stoner Ave.
Los Angeles, CA  90066

Tel: (310) 391-9971
Fax: (310) 391-8971
Web: www.StatModel.com
Support: Support@StatModel.com

Copyright (c) 1998-2019 Muthen & Muthen
